(* Js_coroutine: a computation that can stop in its middle and go on
   later -- what an async function is at each await.

   The interpreter walks the tree by OCaml's own calls (Js_eval): where
   a script is, is where OCaml's stack is. An "await p" deep in a
   loop in a try in a function must stop there, give the hand back to
   whoever called the function, and go on from that very place when p
   is settled, perhaps seconds later. That is a second stack, kept
   aside: a coroutine.

       the caller                       the body
       ----------                       --------
       resume co  ------------------->  runs ...
                                        ... await: suspend ()
       goes on  <---------------------
       ...
       resume co  ------------------->  ... goes on after its await
                                        ends
       goes on  <---------------------

   One of the two runs at a time, never both: [resume] returns when
   the body suspends or ends; [suspend] returns when the body is
   resumed. So nothing of the interpreter needs a lock.

   others:
   OCaml 4.14 has no stack to set aside but a thread's: a coroutine
   here is a thread, held by a mutex and a condition so that it only
   runs between a [resume] and the [suspend] that follows. (OCaml 5's
   effects do it with no thread; the other ways are an interpreter
   written in continuation-passing style, every rule of Js_eval turned
   inside out, or the function's tree rewritten as a state machine,
   what compilers to ES5 do.) A body that is never resumed -- an await
   on a promise nobody settles -- is a thread asleep until the program
   ends: its stack is the price, a few pages of memory.

   Worked example (tests/js/Unit_js_coroutine.ml):

     let log = ref [] in
     let say x = log := x :: !log in
     let co = create (fun () -> say "a"; suspend (); say "c") in
     resume co; say "b"; resume co; say "d"
     (* a b c d *)

   Reference: Conway, "Design of a Separable Transition-Diagram
   Compiler" (1963), where the word is from; ECMA-262 section 27.7
   (async functions), whose "execution context suspended" this is. *)

type t

(* [create body]: not started; [body] runs at the first [resume]. An
 * exception [body] lets through ends it, silently: catch in it what
 * matters *)
val create : (unit -> unit) -> t

(* [resume co]: its body runs, from its start or from where it
 * suspended, until it suspends or ends. One that has ended: nothing *)
val resume : t -> unit

(* from a body running: stop here, and give the hand back to the
 * [resume] that made it run. Outside one: Invalid_argument *)
val suspend : unit -> unit

(* the body running, if one is: what [suspend] would stop, to [resume]
 * later *)
val current : unit -> t option
